Class SequenceRangeRule
    Inherits AttributeRule
    ''' <summary>
    ''' the rule checks of the items in a sequence item attribute are falling between a certain range
    ''' the range can also be one value (for example, from 2 to 2)
    ''' </summary>
    ''' <param name="sequenceItemTag">the sequence item attribute that should have a certain amount of items in it</param>
    ''' <param name="minValue">minimum number of items in the sequence item</param>
    ''' <param name="maxValue">maximum number of items in the sequence item</param>
    ''' <param name="iodType"></param>
    ''' <param name="severity"></param>
    ''' <remarks></remarks>

    Public Sub New(ByVal sequenceItemTag As String, ByVal minValue As Integer, ByVal maxValue As Integer, ByVal iodType As IODType, ByVal severity As ErrorSeverity)

        MyBase.New("Sequence Range Rule", sequenceItemTag, iodType, severity)

        m_minValue = minValue
        m_maxValue = maxValue

    End Sub

    Private m_minValue As Integer
    Private m_maxValue As Integer

    Public Overrides Sub ApplyRule(ByRef dataSet As DvtkHighLevelInterface.Dicom.Messages.DicomMessage, ByVal iodType As IODType)

        If iodType = IODTypeRule Then

            If Not dataSet.Exists(m_TagSequence) Then

                Dim message As String = String.Format("The attribute '{0}' has no items; expected number of items range was {1}-{2}.", Tags.GetTagName(m_TagSequence), m_minValue, m_maxValue)
                If m_minValue = 0 Then
                    Me.SetValid(message)
                Else
                    Me.SetError(message)
                End If

            Else

                Dim sequenceItems As Integer = dataSet.DataSet.GetitemCount(m_TagSequence)
                Dim message As String = String.Format("The attribute '{0}'({1}) has {2} number of items; expected number of items range was {3}-{4}.", Tags.GetTagName(m_TagSequence), m_TagSequence, sequenceItems, m_minValue, m_maxValue)

                If (sequenceItems >= m_minValue And sequenceItems <= m_maxValue) Then
                    Me.SetValid(message)
                Else
                    Me.SetError(message)
                End If

            End If

            Me.Report()

        End If

    End Sub

End Class