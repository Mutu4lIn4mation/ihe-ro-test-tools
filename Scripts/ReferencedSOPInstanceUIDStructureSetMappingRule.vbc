Class ReferencedSOPInstanceUIDStructureSetMappingRule
    Inherits MappingRule
    Protected Const RtStruct As String = "1.2.840.10008.5.1.4.1.1.481.3"


    Public Sub New(ByVal severity As ErrorSeverity, ByVal iodType As IODType)

        MyBase.New("Referenced SOP Instance UID Structure Set Mapping Rule", iodType, severity)

    End Sub

    Public Overrides Sub ApplyRule(ByRef dicomMessage As DvtkHighLevelInterface.Dicom.Messages.DicomMessage, ByVal iodType As IODType)
        Dim referencedSOPInstanceUIDAttribute As DvtkHighLevelInterface.Dicom.Other.Attribute = Nothing
        Dim ImageDataSet As DvtkHighLevelInterface.Dicom.Other.DataSet = Nothing
        Dim referencedSOPInstanceUIDTag As String = "0x30060010[1]/0x30060012[1]/0x00081155"
        Dim studyInstanceUIDTag As String = "0x0020000D"

        If iodType = Me.IODTypeRule Then
            Dim message As String = ""
            'get the corresponding object from the reference test data (by matching sop instance uid)
            Dim receivedDataset As DvtkHighLevelInterface.Dicom.Other.DataSet
            receivedDataset = GetCorrespondingRefDatasetObject(dicomMessage.DataSet)

            If Not receivedDataset.Exists(referencedSOPInstanceUIDTag) Then
                message = "Referenced SOP Instance UID (0008,1155) is not present"
                Me.SetError(message)
            Else
                referencedSOPInstanceUIDAttribute = receivedDataset(referencedSOPInstanceUIDTag)
                ImageDataSet = ReferenceDataSet.GetInstance.GetSliceImage(1, 1, iodType.CTImage).DataSet
                If ImageDataSet Is Nothing Then
                    ImageDataSet = ReferenceDataSet.GetInstance.GetSliceImage(1, 1, iodType.MRImage).DataSet
                    If ImageDataSet Is Nothing Then
                        ImageDataSet = ReferenceDataSet.GetInstance.GetSliceImage(1, 1, iodType.PETImage).DataSet
                        If ImageDataSet Is Nothing Then
                            message = "No Ct, MR or PET image found in the reference dataset"
                            Me.SetError(message)
                        End If
                    End If
                End If

                If Not ImageDataSet Is Nothing Then
                    If ImageDataSet(studyInstanceUIDTag).Values.Equals(referencedSOPInstanceUIDAttribute.Values) Then
                        message = "Referenced SOP Instance UID (0008,1155) matches the Study Instance UID (0020,000D)"
                        Me.SetValid(message)
                    Else
                        message = "Referenced SOP Instance UID (0008,1155) is not equal to the Study Instance UID (0020,000D)"
                        Me.SetError(message)
                    End If
                End If
            End If
        End If

        'Reporter, write to results, if there is anything to report
        Me.Report()
    End Sub
    'this method retrieves the corresponding dataset object from the reference data set, based on its sop class uid
    Protected Function GetCorrespondingRefDatasetObject(ByVal messageDataSet As DvtkHighLevelInterface.Dicom.Other.DataSet) As DvtkHighLevelInterface.Dicom.Other.DataSet
        Dim refDatasetObject As DvtkHighLevelInterface.Dicom.Messages.DicomMessage
        Dim messageSOPClassUID As String
        Dim messageSOPInstanceUID As String

        'Determine message sop class uid
        If Not messageDataSet.Exists("0x00080016") Then
            Logger.GetInstance.LogMessage("Message received does not have a SOP Class UID attribute")
            Return Nothing
        End If

        'Determine message sop instance uid
        If Not messageDataSet.Exists("0x00080018") Then
            Logger.GetInstance.LogMessage("Message received does not have a SOP Instance UID attribute")
            Return Nothing
        End If
        messageSOPClassUID = messageDataSet("0x00080016").Values.Item(0)
        messageSOPInstanceUID = messageDataSet("0x00080018").Values.Item(0)
        'remove any spaces in the sop class & instance uid
        Dim space As Char = " "c
        messageSOPClassUID = messageSOPClassUID.TrimStart(space)
        messageSOPClassUID = messageSOPClassUID.TrimEnd(space)
        messageSOPInstanceUID = messageSOPInstanceUID.TrimStart(space)
        messageSOPInstanceUID = messageSOPInstanceUID.TrimEnd(space)

        'Now we compare the received UID with all the SOPInstaceUID's in the ReferenceTestDataset
        If messageSOPClassUID = RtStruct Then
            Logger.GetInstance.LogMessage("Looking up RTStructure set with sop instance uid '" + messageSOPInstanceUID + "'in scenario reference test data")
            refDatasetObject = ReferenceDataSet.GetInstance.GetStructureSet()

            Dim dataObject = refDatasetObject.DataSet()
            Dim refDatasetObjectSOPInstanceUID As String = dataObject("0x00080018").Values.Item(0).ToString()
            'remove any spaces in the sop instance uid
            refDatasetObjectSOPInstanceUID = refDatasetObjectSOPInstanceUID.TrimStart(space)
            refDatasetObjectSOPInstanceUID = refDatasetObjectSOPInstanceUID.TrimEnd(space)
            If messageSOPInstanceUID = refDatasetObjectSOPInstanceUID Then
                Return dataObject
            End If
        Else
            Return Nothing
        End If
    End Function
End Class